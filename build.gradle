plugins {
    id 'maven-publish'
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'com.wairesd.dceverydaycase'
version = '2.0'

repositories {
    mavenCentral()
    maven { url = "https://repo.jodex.xyz/releases" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT")
    compileOnly("com.jodexindustries.donatecase:spigot-api:2.1.0.5")
    compileOnly("me.clip:placeholderapi:2.11.6")
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('addon.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            name = "Jodex"
            url = uri("https://repo.jodex.xyz/releases")
            credentials {
                username = findProperty("wairesdRepoUser") ?: System.getenv("WAIRESD_REPO_USER")
                password = findProperty("wairesdRepoPassword") ?: System.getenv("WAIRESD_REPO_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = "com.wairesd.dceverydaycase"
            artifactId = "api"
            version = "2.0"
            from components.java
        }
    }
}

